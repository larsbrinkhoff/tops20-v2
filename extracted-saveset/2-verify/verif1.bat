!VERIF1.BAT	CLEANUP OF TOPS-20 SYSTEM PROGRAM VERIFICATION 
! PACKAGE.		4/15/76
!
@DELETE VERSIO.TXT
!
!***********************************************************
!THIS UNUSUAL BATCH CONTROL SEQUENCE RUNS THE EDIT PROGRAM, AND BY
!RELYING ON VARIOUS ERROR CONDITIONS TO CONTROL 
!LOOPING, IT STRIPS OUT OF THE
!LOG FILE VERIFY.LOG THE VERSION INFORMATION WHICH APPEARS
!IN THAT FILE.  
!
!/C128 ALLOWS SPECIAL CHARACTERS TO BE USED (EACH IS PREFIXED WITH
! AN APOSTROPHE.
!
!/NOVICE ASSURES THAT CERTAIN ERROR MESSAGES WILL COME OUT.  THESE
! ARE PREFIXED WITH A PERCENT SIGN, AND ARE USED TO TERMINATE LOOPS.
!
!/NAME SETS THE NAME OF THE OUTPUT FILE TO VERSIO.TXT
!
@EDIT VERIFY.LOG
/C128
/NOVICE
/NAME:VERSIO.TXT
NP1
@ERROR %
!THIS LOOP REMOVES ANY FORM FEEDS CURRENTLY IN THE FILE
!
EDI50::
!
K/2
@IF (NOERROR) @BACKTO EDI50::
!POSITION AT THE BEGINNING OF THE FILE
.^/^
!
EDI20::
!
!SEARCH FOR THE NEXT OCCURRENCE OF A LINE CONTAINING VERSION INFO.
FPROGRAM IS 
!IF THE SEARCH FAILS WE HAVE FOUND THEM ALL.  THEN GOTO EDI10::
@IF (ERROR) @GOTO EDI10::
!OTHERWISE DELETE EVERYTHING FROM THE LAST FORM FEED TO THE
! LINE PRECEEDING THE LINE WITH VERSION INFO.
D^:.-1
!REPOSITION BACK TO THE LINE WITH THE VERSION INFO IN IT.
..+1
!REPLACE WITH NOTHING THE BEGINNING OF THE LINE UPTO AND 
! INCLUDING 'PROGRAM IS'.
S'%'/')'/PROGRAM IS .
!MOVE POINTER TO THE NEXT LINE.
..+1
!MARK IT WITH A FORM FEED.
M.
!LOOP BACK TO GET NEXT LINE WITH VERSION INFO IN IT.
@BACKTO EDI20::
!AT THIS POINT ALL LINES WITH VERSION INFO HAVE BEEN FOUND.
! EACH ONE IS SURROUNDED BY FORM FEEDS.
!
EDI10::
!
!FIRST DELETE THE TAIL END OF VERIFY.LOG
D.:*/*
Y
NP10
!THIS LOOP DELETES ALL FORM FEEDS FROM THE OUTPUT FILE.
!
EDI30::
!
K/2
@IF (NOERROR) @BACKTO EDI30::
EU
!THIS ENDS THE PROCEDURE TO CREATE VERSIO.TXT,  A FILE WHICH
! CONTAINS THE ACTUAL VERSION INFORMATION WHICH IS NOW TO BE COMPARED
! WITH CONTRL.TXT.
!***********************************************************
@RUN VERCMP.EXE
@PRINT ERRORS.LOG
